# shellcheck shell=bash

function precmd {
	if [[ $? -ne 0 ]] ; then
		PROMPT_TERM_COLOR="%F{red}"
	else
		PROMPT_TERM_COLOR="%F{green}"
	fi
	vcs_info
	set-prompt
	if [[ -z "$WINDOW" ]] ; then
		settitle $(hostname -s):$(pwd)
	else
		echo -ne "\033k$(basename "$(pwd)")\033\\"
	fi
}

function preexec {
	update_user_env
	cmd=${2[(w)1]}
	cmd=${cmd#./}
	if [[ $cmd = ssh ]] ; then cmd="s ${2[(w)2]}" ; fi
	if [[ $cmd = vim ]] ; then cmd="v $(basename -- ${2[(w)$#2]})" ; fi
	if [[ -z "$WINDOW" ]] ; then
		settitle "$(hostname -s):$(pwd) -- $cmd"
	else
		echo -ne "\033k$cmd\033\\"
	fi
}

function update_user_env {
	if [[ -n $TMUX ]] ; then
		update_ssh_auth_sock
		# update_xorg_display
		if [[ -n $VSCODE_IPC_HOOK_CLI ]] && [[ ! -e $VSCODE_IPC_HOOK_CLI ]] ; then
			eval $(tmux show-environment VSCODE_IPC_HOOK_CLI)
			eval $(tmux show-environment VSCODE_GIT_ASKPASS_NODE)
			eval $(tmux show-environment VSCODE_GIT_ASKPASS_MAIN)
			eval $(tmux show-environment VSCODE_GIT_IPC_HANDLE)
			echo "${fg[red]}VS Code env updated.${terminfo[sgr0]}"
		fi
	fi
}
